#!/usr/bin/perl -w


use strict;

use File::Temp qw(tempfile);
use FileHandle;
use FindBin;
use Getopt::Long qw(:config bundling auto_abbrev);


########################################################################
#
#  process command line
#

my $jobs;

my $understood = GetOptions('jobs=i' => \$jobs);

die "Usage: $0 [--jobs=<count>] <first-num> <last-run-num>\n",
    unless $understood && @ARGV == 2;


unless ($jobs) {
    $jobs = 0;
    my $cpuinfo = new FileHandle '/proc/cpuinfo';
    while (<$cpuinfo>) {
	++$jobs if /^processor\t/;
    }
    --$jobs;
}

$jobs = 1 if $jobs < 1;


my ($first, $last) = @ARGV;


########################################################################
#
#  build temporary makefile
#

local ($,, $\) = ("\n", "\n");

my ($handle, $filename) = tempfile(UNLINK => 1);

print $handle "all: $_/stamp" foreach $first .. $last;

print $handle ("%/stamp:",
	       "\ttest ! -e orderly-shutdown",
	       "\techo run \$*: start",
	       "\tmkdir \$*",
	       "\tcd \$* && $FindBin::Bin/run-server",
	       "\techo run \$*: done",
	       "\ttouch \$@");
close $handle;


########################################################################
#
#  start the build
#

# scrub environment in case we are inside a larger makefile
delete @ENV{'MAKEFILES', 'MAKFLAGS'};

# tweak a couple of Rhythmbox-related gconf settings
my $prep = "$FindBin::Bin/prepare-settings";
(system { $prep } $prep) == 0 or die "cannot run $prep: $!\n";

my $datadir = "$FindBin::Bin/../data";
mkdir $datadir;

# do not use exec here; it interferes with tempfile cleanup
system 'nice', 'make', '-s', '-k', '-C', $datadir, '-j', $jobs, '-f', $filename;
exit($? >> 8 || $? & 127);
